include stm8s003._

//
// 'blink + serial output' with timer1 and clock setup
// uses external quarz 8 MHz
//

//003
define led	3

// interrupt vectors
interrupt reset@0x8000: (f32) {0x82000000+start}
interrupt trap:         (f32) {0x82000000+dumb}
interrupt tli:          (f32) {0x82000000+dumb}
interrupt awu:          (f32) {0x82000000+dumb}
interrupt clk:          (f32) {0x82000000+dumb}
interrupt exti0:        (f32) {0x82000000+dumb}
interrupt exti1:        (f32) {0x82000000+dumb}
interrupt exti2:        (f32) {0x82000000+dumb}
interrupt exti3:        (f32) {0x82000000+dumb}
interrupt exti4:        (f32) {0x82000000+dumb}
interrupt canrx:        (f32) {0x82000000+dumb}
interrupt cantx:        (f32) {0x82000000+dumb}
interrupt spi:          (f32) {0x82000000+dumb}
interrupt tim1u:        (f32) {0x82000000+tmr1handler}
interrupt tim1c:        (f32) {0x82000000+dumb}
interrupt tim2u:        (f32) {0x82000000+dumb}
interrupt tim2c:        (f32) {0x82000000+dumb}
interrupt tim3u:        (f32) {0x82000000+dumb}
interrupt tim3c:        (f32) {0x82000000+dumb}
interrupt uart1tx:      (f32) {0x82000000+dumb}
interrupt uart1rx:      (f32) {0x82000000+dumb}
interrupt i2c:          (f32) {0x82000000+dumb}
interrupt uart3tx:      (f32) {0x82000000+dumb}
interrupt uart3rx:      (f32) {0x82000000+dumb}
interrupt adc1:         (f32) {0x82000000+dumb}
interrupt tim4u:        (f32) {0x82000000+dumb}
interrupt flash:        (f32) {0x82000000+dumb}

dumb: iret

tmr1handler:
	if tim1 sr1#0 == 0 otherint			// is this right interrupt?
	tim1 sr1#0 = #0					// drop flag
	! gpioa odr#led
	a=uart1dr					// change led state
	a=#'*'
waittxe: if uart1 sr#7==0 waittxe
	uart1 dr=a
otherint: iret

include sysclockhse.stm8s._

init:   // led port
	gpioa ddr#led = #1                           	// for output     
        gpioa cr1#led = #1                           	// set push-pull
	// timer1
 	tim1 cr2 = #0					// no sync with master
	tim1 smcr = #0					// no sync for slave
	tim1 etr = #0					// external clock disable
	tim1 ier#0 = #1					// change interrupt granted
	tim1 pscrh = #0					// prescaler 50
	tim1 pscrl = #50                                // 
	tim1 cr1#2 = #1                                  // overflow interrupt granted
	tim1 cr1#0 = #1                                  // clock started
	gpiod ddr#5=#1  				// set pin for TX
	gpiod cr1#5=#1
	clk pckenr1=#0xff 				// switch on all perferial
	uart1 brr2 = #0x01 ; uart1 brr1 = #0x34         // 9600
	uart1 cr2=#8;                                   // switch on TX
	ret

// main block
start:	x = #ramend; sp = x                       	// initialise stack pointer (it is assembler!!!)
	externalClock(0)
	call near init
	i=#0						// interrupts switch on
loop:   nop                                             // wait for interrupts
	go near loop					// forever cycle
  
